plugins {
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'io.spring.dependency-management' version "${springBootDependencyManagement}"
    id 'java'
    id 'nebula.release' version "17.1.0"
    id 'nebula.maven-publish' version "18.4.0"
    id 'jacoco'
    id 'maven-publish'
}

allprojects {
    group 'com.droidablebee'
    sourceCompatibility = 11
    targetCompatibility = 11
}

java {
    withJavadocJar()
    withSourcesJar()
}

springBoot {
    buildInfo()
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server"

    implementation 'org.springdoc:springdoc-openapi-ui:1.6.5'
    implementation "com.h2database:h2:2.1.210"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.security:spring-security-test"
}

repositories {
    mavenLocal()
    mavenCentral()
}

test {
    useJUnitPlatform()
    afterSuite { desc, result ->
        if (!desc.parent)
            println("${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)")
    }
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    reports {
        xml.required = true
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/pavelfomin/spring-boot-rest-example"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}